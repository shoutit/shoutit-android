buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.5.0'
        classpath 'io.fabric.tools:gradle:1.+'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.7'
    }
}

repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
}

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'io.fabric'

Properties props = new Properties()
def keystoreProperties = file("keystore.properties")
if (keystoreProperties.exists()) {
    props.load(new FileInputStream(keystoreProperties))
}

project.ext.versionCode = 1
def baseVersionName = "0.0.1"
if (project.hasProperty("versionSuffix")) {
    project.ext.versionSuffix = project.versionSuffix
} else {
    project.ext.versionSuffix = "snapshot"
}
if (project.ext.versionSuffix) {
    project.ext.versionName = "${baseVersionName}-${project.versionSuffix}"
} else {
    project.ext.versionName = "${baseVersionName}"
}

if (project.hasProperty("commitId")) {
    project.ext.commitId = project.commitId
} else {
    project.ext.commitId = "<unknown>"
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.shoutit.app.android"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode project.ext.versionCode
        versionName project.ext.versionName
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        renderscriptTargetApi 23
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        debug {
            storeFile file("appunite-debug.keystore")
        }
        release {
            storeFile file("${props.getProperty('onemedical.key.store', 'appunite-debug.keystore')}")
            storePassword "${props.getProperty('onemedical.key.store.password', 'android')}"
            keyAlias "${props.getProperty('onemedical.key.alias', 'androiddebugkey')}"
            keyPassword "${props.getProperty('onemedical.key.alias.password', 'android')}"
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            ext.enableCrashlytics = false
            applicationIdSuffix '.debug'
            versionNameSuffix "-debug"
        }
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            ext.enableCrashlytics = true
        }
    }

    productFlavors {
        dev {
            minSdkVersion 21
        }

        prod {
            minSdkVersion 15
        }
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'LICENSE.txt'
        exclude 'LICENSE'
        exclude 'NOTICE.txt'
        exclude 'NOTICE'
        exclude 'asm-license.txt'
    }

    lintOptions {
        abortOnError true
        disable 'InvalidPackage'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Supports
    compile 'com.android.support:support-v4:23.1.1'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.android.support:cardview-v7:23.1.1'
    compile 'com.android.support:recyclerview-v7:23.1.1'

    // Google
    apt 'com.google.dagger:dagger-compiler:2.0.1'
    compile 'com.google.dagger:dagger:2.0.1'
    compile 'com.google.code.gson:gson:2.5'

    // Square
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.squareup.retrofit2:retrofit:2.0.0-beta3'

    compile 'com.squareup.okhttp:okhttp-urlconnection:2.2.0'

    // RX
    compile 'com.trello:rxlifecycle:0.4.0'
    compile 'com.trello:rxlifecycle-components:0.4.0'
    compile 'com.jakewharton.rxbinding:rxbinding:0.3.0'
    compile 'com.jakewharton.rxbinding:rxbinding-recyclerview-v7:0.3.0'
    compile 'com.jakewharton.rxbinding:rxbinding-design:0.3.0'
    compile('com.jakewharton.rxbinding:rxbinding-appcompat-v7:0.2.0') {
        exclude group: 'com.android.support', module: 'appcompat-v7'
    }
    compile('com.jakewharton.rxbinding:rxbinding-support-v4:0.2.0') {
        exclude group: 'com.android.support', module: 'appcompat-v4'
    }
    compile(project(":rx-android-extensions")) {
        exclude group: 'com.google.code.findbugs', module: 'annotations'
    }

    // Tests
    testCompile 'junit:junit:4.12'
    testCompile 'com.android.support:support-annotations:23.1.1'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testApt 'com.google.dagger:dagger-compiler:2.0.1'
    testCompile 'com.google.truth:truth:0.26'

    // Others
    compile('com.crashlytics.sdk.android:crashlytics:2.5.1@aar') {
        transitive = true;
    }
}
