buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
        maven { url "https://jitpack.io" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.0.0-beta6'
        classpath 'io.fabric.tools:gradle:1.21.2'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        classpath 'com.google.gms:google-services:2.0.0-alpha9'
    }
}

repositories {
    jcenter()
    maven { url 'https://maven.fabric.io/public' }
}

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'io.fabric'

Properties props = new Properties()
def keystoreProperties = file("keystore.properties")
if (keystoreProperties.exists()) {
    props.load(new FileInputStream(keystoreProperties))
}

def baseVersionName = "2.0.0"

project.ext.versionCode = 69

if (project.hasProperty("versionSuffix")) {
    project.ext.versionChangeSuffix = project.versionSuffix
} else {
    project.ext.versionChangeSuffix = "-snapshot"
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.shoutit.app.android"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode project.ext.versionCode
        versionName "${baseVersionName}-${project.ext.versionChangeSuffix}"
        renderscriptTargetApi 23
        multiDexEnabled true
        testInstrumentationRunner "com.android.test.runner.MultiDexTestRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        debug {
            storeFile file("appunite-debug.keystore")
        }

        release {
            storeFile file("${props.getProperty('shoutit.key.store', 'appunite-debug.keystore')}")
            storePassword "${props.getProperty('shoutit.key.store.password', 'android')}"
            keyAlias "${props.getProperty('shoutit.key.alias', 'androiddebugkey')}"
            keyPassword "${props.getProperty('shoutit.key.alias.password', 'android')}"
        }
    }

    buildTypes {

        debug {
            debuggable true
            minifyEnabled false
            ext.enableCrashlytics = false
            applicationIdSuffix '.debug'
            versionNameSuffix "-debug"
            buildConfigField "String", "API_URL", dev_runscope_api_url
        }

        staging {
            signingConfig signingConfigs.debug
            debuggable true
            minifyEnabled false
            ext.enableCrashlytics = true
            ext.betaDistributionGroupAliases = "android-internal-testers, internal-tests"
            ext.betaDistributionNotifications = false
            applicationIdSuffix '.staging'
            versionNameSuffix "-staging"
            buildConfigField "String", "API_URL", dev_runscope_api_url
        }

        release {
            debuggable false
            minifyEnabled false
            signingConfig signingConfigs.release
            ext.betaDistributionGroupAliases = "android-internal-testers, internal-tests"
            ext.betaDistributionNotifications = false
            ext.enableCrashlytics = true
            buildConfigField "String", "API_URL", production_api_url
        }
    }

    productFlavors {
        dev {
            minSdkVersion 21
        }

        prod {
            minSdkVersion 16
        }
    }

    android.variantFilter { variant ->
        if (variant.buildType.name.equals('staging') && variant.getFlavors().get(0).name.equals('dev')) {
            variant.setIgnore(true);
        }
        if (variant.buildType.name.equals('release') && variant.getFlavors().get(0).name.equals('dev')) {
            variant.setIgnore(true);
        }
        if (variant.buildType.name.equals('release') && variant.getFlavors().get(0).name.equals('dev')) {
            variant.setIgnore(true);
        }
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'LICENSE.txt'
        exclude 'LICENSE'
        exclude 'NOTICE.txt'
        exclude 'NOTICE'
        exclude 'asm-license.txt'
    }

    lintOptions {
        abortOnError true
        disable 'InvalidPackage'
    }
}

configurations.all {
    resolutionStrategy.force 'com.android.support:support-annotations:23.1.1'
    resolutionStrategy.force 'com.google.guava:guava:18.0'
    resolutionStrategy.force 'com.android.support:support-v4:23.1.1'
    resolutionStrategy.force 'com.android.support:appcompat-v7:23.1.1'
    resolutionStrategy.force 'io.reactivex:rxjava:1.1.0'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Supports
    compile 'com.android.support:support-v4:23.1.1'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.android.support:cardview-v7:23.1.1'
    compile 'com.android.support:recyclerview-v7:23.1.1'
    compile 'com.android.support:support-annotations:23.1.1'
    compile 'com.android.support:multidex:1.0.0'
    compile 'com.github.clans:fab:1.6.1'

    // Facebook
    compile 'com.facebook.android:facebook-android-sdk:4.6.0'

    // Google
    apt 'com.google.dagger:dagger-compiler:2.0.1'
    compile 'com.google.dagger:dagger:2.0.1'
    compile 'com.google.code.gson:gson:2.5'
    compile 'com.google.code.findbugs:jsr305:2.0.1'
    compile 'com.google.android.gms:play-services-location:8.4.0'
    compile 'com.google.guava:guava:18.0'
    compile 'com.google.android.gms:play-services-auth:8.4.0'

    // Square
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.squareup.retrofit2:retrofit:2.0.0-beta3'
    compile 'com.squareup.okhttp3:okhttp-urlconnection:3.0.1'
    compile 'com.squareup.okhttp3:logging-interceptor:3.0.1'
    compile 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.0.2'
    compile 'com.squareup.retrofit2:converter-gson:2.0.0-beta3'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.0.0-beta3'

    // RX
    compile 'com.trello:rxlifecycle:0.4.0'
    compile 'com.trello:rxlifecycle-components:0.4.0'
    compile 'com.jakewharton.rxbinding:rxbinding:0.3.0'
    compile 'com.jakewharton.rxbinding:rxbinding-recyclerview-v7:0.3.0'

    compile(project(":rx-android-extensions")) {
        exclude group: 'com.google.code.findbugs', module: 'annotations'
    }

    // other
    compile 'com.uservoice:uservoice-android-sdk:1.2.4'
    compile 'fr.avianey.com.viewpagerindicator:library:2.4.1@aar'
    compile('com.crashlytics.sdk.android:crashlytics:2.5.1@aar') {
        transitive = true;
    }

    // Tests
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testApt 'com.google.dagger:dagger-compiler:2.0.1'
    testCompile 'com.google.truth:truth:0.26'
    testCompile 'org.powermock:powermock-api-mockito:1.6.4'
    testCompile 'org.powermock:powermock-module-junit4-rule-agent:1.6.4'
    testCompile 'org.powermock:powermock-module-junit4-rule:1.6.4'
    testCompile 'org.powermock:powermock-module-junit4:1.6.4'

    // Amazon
    compile 'com.amazonaws:aws-android-sdk-core:2.2.13'
    compile 'com.amazonaws:aws-android-sdk-cognito:2.2.13'
    compile 'com.amazonaws:aws-android-sdk-s3:2.2.13'
    compile 'com.amazonaws:aws-android-sdk-ddb:2.2.13'
}

apply plugin: 'com.google.gms.google-services'
