buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
        maven { url "https://jitpack.io" }
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.0'
        classpath 'io.fabric.tools:gradle:1.21.2'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        classpath 'com.google.gms:google-services:2.0.0-alpha9'
        classpath 'me.tatarka:gradle-retrolambda:3.3.0-beta3'
        classpath 'me.tatarka.retrolambda.projectlombok:lombok.ast:0.2.3.a2'
        classpath "com.newrelic.agent.android:agent-gradle-plugin:5.+"
    }

    // Exclude the version that the android plugin depends on.
    configurations.classpath.exclude group: 'com.android.tools.external.lombok'
}

repositories {
    jcenter()
    maven { url "https://s3.amazonaws.com/repo.commonsware.com" }
    maven { url 'https://maven.fabric.io/public' }
    maven { url 'http://clojars.org/repo' }
    maven { url "https://jitpack.io" }
    mavenCentral()
}

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'io.fabric'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'newrelic'

Properties props = new Properties()
def keystoreProperties = file("keystore.properties")
if (keystoreProperties.exists()) {
    props.load(new FileInputStream(keystoreProperties))
}

def baseVersionName = "3.0.4"

project.ext.versionCode = 89

if (project.hasProperty("versionSuffix")) {
    project.ext.versionChangeSuffix = project.versionSuffix
} else {
    project.ext.versionChangeSuffix = "-snapshot"
}

if (project.hasProperty("commitId")) {
    project.ext.commitId = project.commitId
} else {
    project.ext.commitId = "localBuildCommtId"
}

if (project.hasProperty("buildNumber")) {
    project.ext.buildNumber = project.buildNumber
} else {
    project.ext.buildNumber = "localBuildNumber"
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    dexOptions {
        dexInProcess true
    }

    defaultConfig {
        applicationId "com.shoutit.app.android"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode project.ext.versionCode
        versionName "${baseVersionName}-${project.ext.versionChangeSuffix}"
        renderscriptTargetApi 23
        multiDexEnabled true
        testInstrumentationRunner "com.android.test.runner.MultiDexTestRunner"
        buildConfigField 'String', 'commitId', "\"${project.ext.commitId}\""
        buildConfigField 'String', 'buildNumber', "\"${project.ext.buildNumber}\""
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        debug {
            storeFile file("appunite-debug.keystore")
        }

        release {
            storeFile file("${props.getProperty('shoutit.key.store', 'appunite-debug.keystore')}")
            storePassword "${props.getProperty('shoutit.key.store.password', 'android')}"
            keyAlias "${props.getProperty('shoutit.key.alias', 'androiddebugkey')}"
            keyPassword "${props.getProperty('shoutit.key.alias.password', 'android')}"
        }
    }

    buildTypes {

        debug {
            debuggable true
            minifyEnabled false
            ext.enableCrashlytics = false
            applicationIdSuffix '.debug'
            versionNameSuffix "-debug"
            buildConfigField "String", "API_URL", dev_runscope_api_url
            buildConfigField 'Boolean', 'enableCrashlytics', 'false'
        }

        staging {
            signingConfig signingConfigs.debug
            debuggable true
            minifyEnabled false
            ext.enableCrashlytics = true
            ext.betaDistributionGroupAliases = "android-internal-testers, internal-tests"
            ext.betaDistributionNotifications = false
            applicationIdSuffix '.staging'
            versionNameSuffix "-staging"
            buildConfigField "String", "API_URL", dev_runscope_api_url
            buildConfigField 'Boolean', 'enableCrashlytics', 'true'
        }

        local {
            signingConfig signingConfigs.debug
            debuggable true
            minifyEnabled false
            ext.enableCrashlytics = true
            ext.betaDistributionGroupAliases = "android-internal-testers, internal-tests"
            ext.betaDistributionNotifications = false
            applicationIdSuffix '.local'
            versionNameSuffix "-local"
            buildConfigField "String", "API_URL", local_api_url
            buildConfigField 'Boolean', 'enableCrashlytics', 'true'
        }

        release {
            debuggable false
            minifyEnabled false
            signingConfig signingConfigs.release
            ext.betaDistributionGroupAliases = "android-internal-testers, internal-tests"
            ext.betaDistributionNotifications = false
            ext.enableCrashlytics = true
            buildConfigField "String", "API_URL", production_api_url
            buildConfigField 'Boolean', 'enableCrashlytics', 'true'
        }
    }

    productFlavors {
        dev {
            minSdkVersion 21
        }

        prod {
            minSdkVersion 16
        }
    }

    android.variantFilter { variant ->
/*        if (variant.buildType.name.equals('staging') && variant.getFlavors().get(0).name.equals('dev')) {
            variant.setIgnore(true);
        }*/
        if (variant.buildType.name.equals('release') && variant.getFlavors().get(0).name.equals('dev')) {
            variant.setIgnore(true);
        }
        if (variant.buildType.name.equals('debug') && variant.getFlavors().get(0).name.equals('prod')) {
            variant.setIgnore(true);
        }
        if (variant.buildType.name.equals('local') && variant.getFlavors().get(0).name.equals('dev')) {
            variant.setIgnore(true);
        }
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'LICENSE.txt'
        exclude 'LICENSE'
        exclude 'NOTICE.txt'
        exclude 'NOTICE'
        exclude 'asm-license.txt'
    }

    lintOptions {
        abortOnError true
        disable 'InvalidPackage', 'MissingTranslation', 'StringFormatInvalid'
    }
}

configurations.all {
    resolutionStrategy.force 'com.android.support:support-annotations:23.1.1'
    resolutionStrategy.force 'com.google.guava:guava:18.0'
    resolutionStrategy.force 'com.android.support:support-v4:23.1.1'
    resolutionStrategy.force 'com.android.support:appcompat-v7:23.1.1'
    resolutionStrategy.force 'io.reactivex:rxjava:1.1.0'
    resolutionStrategy.force 'com.android.support:cardview-v7:23.1.1'
    resolutionStrategy.force 'com.android.support:customtabs:23.1.1'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Supports
    compile 'com.android.support:support-v4:23.1.1'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.android.support:cardview-v7:23.1.1'
    compile 'com.android.support:recyclerview-v7:23.1.1'
    compile 'com.android.support:support-annotations:23.1.1'
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.github.clans:fab:1.6.1'

    // Facebook
    compile('com.facebook.android:facebook-android-sdk:4.12.1') {
        transitive = true;
    }
    compile 'com.facebook.android:audience-network-sdk:4.12.1'

    // Google
    provided 'javax.annotation:jsr250-api:1.0'
    apt 'com.google.dagger:dagger-compiler:2.0.1'
    compile 'com.google.dagger:dagger:2.0.1'
    compile 'com.google.code.gson:gson:2.5'
    compile 'com.google.code.findbugs:jsr305:2.0.1'
    compile 'com.google.android.gms:play-services-location:8.4.0'
    compile 'com.google.guava:guava:18.0'
    compile 'com.google.android.gms:play-services-auth:8.4.0'

    // Square
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.squareup.retrofit2:retrofit:2.0.0-beta3'
    compile 'com.squareup.okhttp3:okhttp-urlconnection:3.0.1'
    compile 'com.squareup.okhttp3:logging-interceptor:3.0.1'
    compile 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.0.2'
    compile 'com.squareup.retrofit2:converter-gson:2.0.0-beta3'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.0.0-beta3'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-beta2'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'
    stagingCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'
    localCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'

    // RX
    compile 'com.trello:rxlifecycle:0.3.0'
    compile 'com.trello:rxlifecycle-components:0.3.0'
    compile 'com.jakewharton.rxbinding:rxbinding:0.3.0'
    compile 'com.jakewharton.rxbinding:rxbinding-recyclerview-v7:0.3.0'
    compile 'com.github.jacek-marchwicki.rx-java-extensions:rx-extensions:9906cc7974'
    compile 'com.github.jacek-marchwicki.rx-java-extensions:rx-android-extensions:9906cc7974'

    // Tests
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testApt 'com.google.dagger:dagger-compiler:2.0.1'
    testCompile 'com.google.truth:truth:0.26'
    testCompile 'org.powermock:powermock-api-mockito:1.6.4'
    testCompile 'org.powermock:powermock-module-junit4-rule-agent:1.6.4'
    testCompile 'org.powermock:powermock-module-junit4-rule:1.6.4'
    testCompile 'org.powermock:powermock-module-junit4:1.6.4'

    androidTestCompile 'org.mockito:mockito-core:1.10.19'
    androidTestCompile 'com.google.truth:truth:0.26'

    // Amazon
    compile 'com.amazonaws:aws-android-sdk-core:2.2.13'
    compile 'com.amazonaws:aws-android-sdk-cognito:2.2.13'
    compile 'com.amazonaws:aws-android-sdk-s3:2.2.13'
    compile 'com.amazonaws:aws-android-sdk-ddb:2.2.13'

    // other
    compile 'com.twilio:conversations-android:0.12.1'
    compile 'com.uservoice:uservoice-android-sdk:1.2.4'
    compile 'fr.avianey.com.viewpagerindicator:library:2.4.1@aar'
    compile('com.crashlytics.sdk.android:crashlytics:2.5.1@aar') {
        transitive = true;
    }
    compile 'com.commonsware.cwac:cam2:0.6.5'
    compile 'com.github.pusher:pusher-websocket-java:f33bc9ac42'
    compile "com.mixpanel.android:mixpanel-android:4.8.7"
    compile project(":FFmpegAndroid")
    compile project(":appuniteGcm")
    compile project(":ScrollViewGallery")
    compile 'com.newrelic.agent.android:android-agent:5.+'

}

apply plugin: 'com.google.gms.google-services'
